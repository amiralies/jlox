program        ::= { declaration } EOF ;

declaration    ::= varDecl
               | statement ;

varDecl        ::= "var" IDENTIFIER [ "=" expression ] ";" ;

statement      ::= exprStmt
               | ifStmt
               | printStmt
               | block ;

ifStmt         ::= "if" "(" expression ")" statement [ "else" statement ];

block          ::= "{" { declaration } "}" ;

exprStmt       ::= expression ";" ;

printStmt      ::= "print" expression ";" ;

expression     ::= commaSequence ;

commaSequence  ::= assignment { "," assignment } ;

assignment     ::= IDENTIFIER "=" commaSequence
               | conditional ;

conditional    ::= equality { "?" assignment ":" assignment } ;

equality       ::= comparison { ( "!=" | "==" ) comparison } ;

comparison     ::= term { ( ">" | ">=" | "<" | "<=" ) term } ;

term           ::= factor { ( "-" | "+" ) factor } ;

factor         ::= unary { ( "/" | "*" ) unary } ;

unary          ::= ( "!" | "-" ) unary
               | primary ;

primary        ::= NUMBER | STRING | "true" | "false" | "nil"
               | "(" expression ")"
			   | IDENTIFIER ;
